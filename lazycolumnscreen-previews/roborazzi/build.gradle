plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'io.github.takahirom.roborazzi'
}

android {
    namespace 'snapshot.testing.lazycolumn_previews.roborazzi'
    compileSdk 34

    defaultConfig {
        minSdk 23

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                // Do not run out of memory when running Roborazzi tests for different api levels
                jvmArgs '-Xmx1g'

                // Enable running tests in parallel
                if (project.hasProperty("parallel")) {
                    maxParallelForks = Runtime.runtime.availableProcessors() / 2
                }

                // Enable hardware rendering to display shadows and elevation. Still experimental
                // Supported only on API 28+
                systemProperty 'robolectric.pixelCopyRenderMode', 'hardware'
            }
        }
    }
}

dependencies {
    implementation project(':lazycolumnscreen')

    // Jetpack Compose
    implementation platform('androidx.compose:compose-bom:2024.05.00')
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.animation:animation"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"

    testImplementation "org.robolectric:robolectric:4.13"

    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.5.0'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:robolectric:2.5.0'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:roborazzi:2.5.0'

    testImplementation 'io.github.takahirom.roborazzi:roborazzi-compose:1.26.0'
    testImplementation 'io.github.sergio-sastre.ComposablePreviewScanner:android:0.4.0'
}