plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'io.github.takahirom.roborazzi'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'snapshot.testing.lazycolumn_previews.roborazzi'
    compileSdk 36

    defaultConfig {
        minSdk 23

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                // Do not run out of memory when running Roborazzi tests for different api levels
                jvmArgs '-Xmx1g'

                // Enable running tests in parallel
                if (project.hasProperty("parallel")) {
                    maxParallelForks = Runtime.runtime.availableProcessors() / 2
                }

                // Enable hardware rendering to display shadows and elevation. Still experimental
                // Supported only on API 28+
                systemProperty 'robolectric.pixelCopyRenderMode', 'hardware'
            }
        }
    }
}

dependencies {
    implementation project(':lazycolumnscreen')

    // Jetpack Compose
    implementation platform('androidx.compose:compose-bom:2025.05.01')
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.animation:animation"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-test-manifest"

    testImplementation "org.robolectric:robolectric:4.16"

    testImplementation 'com.google.testparameterinjector:test-parameter-injector:1.19'
    testImplementation 'io.github.takahirom.roborazzi:roborazzi-compose:1.50.0'
    testImplementation 'io.github.takahirom.roborazzi:roborazzi-compose-preview-scanner-support:1.50.0'
    testImplementation 'io.github.sergio-sastre.ComposablePreviewScanner:android:0.7.1'
}