import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'dev.testify'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.0'
}

ext {
    SCREENSHOT_ANNOTATION = "screenshotAnnotation"
    RECORD_MODE_GMD = "recordModeGmd"
    USE_TEST_STORAGE = "useTestStorage"
}

String filterAnnotation() {
    if (project.hasProperty(SCREENSHOT_ANNOTATION)) {
        return project.property(SCREENSHOT_ANNOTATION)
    } else {
        return null // fallbacks to default annotation
    }
}


android {
    namespace 'snapshot.testing.recyclerview_previews.android_testify'
    compileSdk 35

    defaultConfig {
        testApplicationId "com.example.road.to.effective.snapshot.testing"
        minSdk 23

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments = [
                // for Gradle Managed Devices
                useTestStorageService: "true"
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    // Not reliable on every API. Seems to work only from API 25 on.
    // Better to disable through developer settings
    testOptions {
        if (project.hasProperty("useOrchestrator")) {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
        animationsDisabled = true

        // Warning: android-testify does not support shards with gradle managed devices for now
        managedDevices {
            devices {
                pixel3api30(ManagedVirtualDevice) {
                    device = "Pixel 3"
                    apiLevel = 30
                    systemImageSource = "aosp" // do not use atd images for screenshot testing
                }
            }
        }
    }
}


testify {
    moduleName ":recyclerviewscreen-previews:android-testify"
    applicationPackageId 'com.example.road.to.effective.snapshot.testing'
    testPackageId 'com.example.road.to.effective.snapshot.testing'
    screenshotAnnotation = filterAnnotation()
    // for Gradle Managed Devices
    recordMode = project.hasProperty(RECORD_MODE_GMD)
    useTestStorage = project.hasProperty(USE_TEST_STORAGE)
}

// Create task to move screenshots generated via Gradle Managed Devices to the right location
// before verifying them
def devices = project.hasProperty("devices") ? project.devices : "pixel3api30"

tasks.register('copyScreenshots', Copy) {
    from "build/outputs/managed_device_android_test_additional_output/debug/${devices}/images"
    into "src/androidTest/assets"
}

dependencies {
    implementation project(":recyclerviewscreen")

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'

    // Jetpack Compose
    implementation platform('androidx.compose:compose-bom:2025.02.00')
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.animation:animation"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"

    androidTestImplementation ('androidx.navigation:navigation-compose:2.8.7') {
        because "The ActivityScenarioRule wants to set a Composable as activity content"
    }
    androidTestImplementation 'com.google.testparameterinjector:test-parameter-injector:1.18'
    androidTestImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.6.0'
    androidTestImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:android-testify:2.6.0'
    debugImplementation 'io.github.sergio-sastre.ComposablePreviewScanner:android:0.5.1'

    // noinspection GradleDependency
    androidTestImplementation('androidx.test:rules:1.5.0') {
        because 'newer versions make Android-testify crash'
    }
    // noinspection GradleDependency
    androidTestImplementation ('androidx.test.espresso:espresso-core:3.5.1'){
        because 'newer versions make Android-testify crash'
    }
    // for Gradle Manage Devices
    androidTestUtil('androidx.test.services:test-services:1.5.0')
}