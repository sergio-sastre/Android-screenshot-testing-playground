plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'shot'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.lazycolumnscreen.shot'
    compileSdk 36

    defaultConfig {
        testApplicationId "com.example.road.to.effective.snapshot.testing"
        minSdk 23
        targetSdk 36

        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    

    // Not reliable on every API. Seems to work only from API 25 on.
    // Better to disable through developer settings
    testOptions {
        if (project.hasProperty("useOrchestrator")) {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
        animationsDisabled = true
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }
}

shot {
    tolerance = 0.15 // Tests pass if less than 0,15% of the pixels differ
    applicationId = "com.example.road.to.effective.snapshot.testing"
}

dependencies {
    implementation project(':testannotations')
    implementation project(':lazycolumnscreen')

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.navigation:navigation-compose:2.9.5'

    // Jetpack Compose
    androidTestImplementation platform('androidx.compose:compose-bom:2025.10.00')
    androidTestImplementation "androidx.compose.material:material"

    androidTestImplementation 'com.google.testparameterinjector:test-parameter-injector:1.19'
    androidTestImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.7.0'

    androidTestUtil 'androidx.test:orchestrator:1.6.1'
}
