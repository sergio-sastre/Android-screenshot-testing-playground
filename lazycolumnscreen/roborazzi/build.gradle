plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'io.github.takahirom.roborazzi'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.0'
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.lazycolumnscreen.roborazzi'
    compileSdk 35

    defaultConfig {
        testApplicationId "com.example.road.to.effective.snapshot.testing"
        minSdk 23
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                // Do not run out of memory when running Roborazzi tests for different api levels
                jvmArgs '-Xmx1g'

                // Enable running tests in parallel
                if (project.hasProperty("parallel")) {
                    maxParallelForks = Runtime.runtime.availableProcessors() / 2
                }

                // Enable hardware rendering to display shadows and elevation. Still experimental
                // Supported only on API 28+
                systemProperty 'robolectric.pixelCopyRenderMode', 'hardware'
            }
        }
    }
}

dependencies {
    implementation project(':lazycolumnscreen')

    implementation "androidx.navigation:navigation-compose:2.8.5"

    // Jetpack Compose
    testImplementation platform('androidx.compose:compose-bom:2025.01.00')
    testImplementation "androidx.compose.material:material"

    testImplementation "org.robolectric:robolectric:4.14.1"

    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.6.0'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:robolectric:2.6.0'
    testImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:roborazzi:2.6.0'

    //testImplementation 'io.github.darkxanter:webp-imageio:0.3.3'
}
