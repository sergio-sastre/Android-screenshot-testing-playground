import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'dev.testify'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.0'
}

ext {
    SCREENSHOT_ANNOTATION = "screenshotAnnotation"
    RECORD_MODE_GMD = "recordModeGmd"
    USE_TEST_STORAGE = "useTestStorage"
}

String filterAnnotation() {
    if (project.hasProperty(SCREENSHOT_ANNOTATION)) {
        return project.property(SCREENSHOT_ANNOTATION)
    } else {
        return null // fallbacks to default annotation
    }
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.lazycolumnscreen.android_testify'
    compileSdk 35

    defaultConfig {
        testApplicationId "com.example.road.to.effective.snapshot.testing"
        minSdk 23
        targetSdk 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments = [
                // for Gradle Managed Devices
                useTestStorageService: "true"
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Not reliable on every API. Seems to work only from API 25 on.
    // Better to disable through developer settings
    testOptions {
        if (project.hasProperty("useOrchestrator")) {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
        animationsDisabled = true

        managedDevices {
            devices {
                pixel3api30(ManagedVirtualDevice) {
                    device = "Pixel 3"
                    apiLevel = 30
                    systemImageSource = "aosp" // do not use atd images for screenshot testing
                }
            }
        }
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }
}

testify {
    moduleName = ":lazycolumnscreen:android-testify"
    applicationPackageId = 'com.example.road.to.effective.snapshot.testing'
    testPackageId = 'com.example.road.to.effective.snapshot.testing'
    screenshotAnnotation = filterAnnotation()

    // for Gradle Managed Devices
    recordMode = project.hasProperty(RECORD_MODE_GMD)
    useTestStorage = project.hasProperty(USE_TEST_STORAGE)
}

// Create task to move screenshots generated via Gradle Managed Devices to the right location
// before verifying them
def devices = project.hasProperty("devices") ? project.devices : "pixel3api30"

tasks.register('copyScreenshots', Copy) {
    from "build/outputs/managed_device_android_test_additional_output/debug/${devices}/images"
    into "src/androidTest/assets"
}

dependencies {
    implementation project(':lazycolumnscreen')
    implementation project(':testannotations')

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.navigation:navigation-compose:2.9.0'

    // Jetpack Compose
    androidTestImplementation platform('androidx.compose:compose-bom:2025.05.01')
    androidTestImplementation 'androidx.compose.material:material'

    androidTestImplementation 'com.google.testparameterinjector:test-parameter-injector:1.18'

    androidTestImplementation 'androidx.test:rules:1.6.1'

    androidTestImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.6.0'
    androidTestImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:android-testify:2.5.0'

    androidTestUtil 'androidx.test:orchestrator:1.5.1'
    androidTestUtil 'androidx.test.services:test-services:1.5.0'
}