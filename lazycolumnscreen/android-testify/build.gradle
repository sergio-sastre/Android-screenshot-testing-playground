import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'dev.testify'
}

ext {
    SCREENSHOT_ANNOTATION = "screenshotAnnotation"
    RECORD_MODE_GMD = "recordModeGmd"
}

String filterAnnotation() {
    if (project.hasProperty(SCREENSHOT_ANNOTATION)) {
        return project.property(SCREENSHOT_ANNOTATION)
    } else {
        return null // fallbacks to default annotation
    }
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.lazycolumnscreen.android_testify'
    compileSdk 34

    defaultConfig {
        testApplicationId "com.example.road.to.effective.snapshot.testing"
        minSdk 23
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments = [
                // for Gradle Managed Devices
                useTestStorageService: "true"
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Not reliable on every API. Seems to work only from API 25 on.
    // Better to disable through developer settings
    testOptions {
        if (project.hasProperty("useOrchestrator")) {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
        animationsDisabled = true

        // Warning: android-testify does not support shards with gradle managed devices for now
        managedDevices {
            devices {
                pixel3api30(ManagedVirtualDevice) {
                    device = "Pixel 3"
                    apiLevel = 30
                    systemImageSource = "aosp" // do not use atd images for screenshot testing
                }
            }
        }
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
}

testify {
    moduleName = ":lazycolumnscreen:android-testify"
    applicationPackageId = 'com.example.road.to.effective.snapshot.testing'
    testPackageId = 'com.example.road.to.effective.snapshot.testing'
    screenshotAnnotation = filterAnnotation()

    // for Gradle Managed Devices
    recordMode = project.hasProperty(RECORD_MODE_GMD)
    useTestStorage = true
}

dependencies {
    implementation project(':lazycolumnscreen')
    implementation project(':testannotations')

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.navigation:navigation-compose:2.7.5"

    // Jetpack Compose
    androidTestImplementation platform('androidx.compose:compose-bom:2023.03.00')
    androidTestImplementation "androidx.compose.material:material"

    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation "dev.testify:testify-compose:2.0.0-rc03"
    androidTestImplementation 'com.google.testparameterinjector:test-parameter-injector:1.8'

    androidTestImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.1.0'
    androidTestImplementation 'com.github.sergio-sastre.AndroidUiTestingUtils:android-testify:2.1.0'

    androidTestUtil 'androidx.test:orchestrator:1.4.2'
    // for Gradle Managed Devices
    androidTestUtil "androidx.test.services:test-services:1.4.2"
}