import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.0.0'

    // 1 on-device + 1 JVM screenshot test library
    id 'dev.testify'
    id 'app.cash.paparazzi'
}

// For Android-Testify
ext {
    SCREENSHOT_ANNOTATION = "screenshotAnnotation"
    RECORD_MODE_GMD = "recordModeGmd"
    USE_TEST_STORAGE = "useTestStorage"
}

android {
    namespace 'com.example.road.to.effective.snapshot.testing.lazycolumnscreen.sharedtests'
    compileSdk 35

    defaultConfig {
        testApplicationId "com.example.road.to.effective.snapshot.testing"
        minSdk 23
        targetSdk 35

        // Warning: For Shot, replace it with testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments = [
                // for Gradle Managed Devices
                useTestStorageService: "true"
        ]

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            pseudoLocalesEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }

    testOptions {
        // Not reliable on every API. Seems to work only from API 25 on.
        // Better to disable through developer settings
        animationsDisabled = true

        managedDevices {
            devices {
                pixel3api30(ManagedVirtualDevice) {
                    device = "Pixel 3"
                    apiLevel = 30
                    systemImageSource = "aosp" // do not use atd images for screenshot testing
                }
            }
        }
    }

    // required for shared tests in this module: on-device + JVM screenshot tests
    sourceSets {
        test {
            java.srcDir 'src/sharedTest/java'
        }
        androidTest {
            java.srcDir 'src/sharedTest/java'
        }
    }
}

testify {
    moduleName = ':lazycolumnscreen:android-testify+paparazzi'
    applicationPackageId = 'com.example.road.to.effective.snapshot.testing'
    testPackageId = 'com.example.road.to.effective.snapshot.testing'
    screenshotAnnotation = 'sergio.sastre.uitesting.utils.crosslibrary.annotations.CrossLibraryScreenshot'

    // for Gradle Managed Devices
    recordMode = project.hasProperty(RECORD_MODE_GMD)
    useTestStorage = project.hasProperty(USE_TEST_STORAGE)
}

// Android-Testify
// Create task to move screenshots generated via Gradle Managed Devices to the right location
// before verifying them
def devices = project.hasProperty("devices") ? project.devices : "pixel3api30"

tasks.register('copyScreenshots', Copy) {
    from "build/outputs/managed_device_android_test_additional_output/debug/${devices}/images"
    into "src/androidTest/assets"
}

dependencies {
    implementation project(':lazycolumnscreen')
    implementation project(':testannotations')

    // Jetpack Compose
    debugImplementation platform('androidx.compose:compose-bom:2025.05.01')
    debugImplementation "androidx.compose.material:material"

    debugImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:utils:2.6.0')

    // Support for Android-Testify
    debugImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:android-testify:2.5.0')

    // Support for Paparazzi
    testImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:paparazzi:2.6.0')
    debugImplementation('com.github.sergio-sastre.AndroidUiTestingUtils:mapper-paparazzi:2.6.0')

    // for Gradle Managed Devices
    androidTestUtil "androidx.test.services:test-services:1.5.0"
}
